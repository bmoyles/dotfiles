function prompt_bmoyles_setup {
    emulate -L zsh
    prompt_opts=(cr percent subst)

    autoload -Uz add-zsh-hook
    add-zsh-hook precmd prompt_bmoyles_precmd

    autoload -Uz vcs_info
    zstyle ':vcs_info:*' enable git
    zstyle ':vcs_info:*' max-exports 5
    zstyle ':vcs_info:git*' check-for-changes true
    zstyle ':vcs_info:*' use-quilt false
    zstyle ':vcs_info:git*' unstagedstr "mod"
    zstyle ':vcs_info:git*' stagedstr "stg"
    zstyle ':vcs_info:git*' formats \
        "%r $chars[branch] %b" \
        "%m" \
        "%u" \
        "%c"
    zstyle ':vcs_info:git*' actionformats \
        "%r $chars[branch] %b" \
        "%m" \
        "%u" \
        "%c" \
        "%a"
    zstyle ':vcs_info:git*+set-message:*' hooks git-st git-remotebranch git-untracked


    PROMPT='%{%f%b%k%}$(prompt_bmoyles_build_prompt)'
    RPROMPT='%{%f%b%k%}$(prompt_bmoyles_build_rprompt)'
}

function +vi-git-st() {
    if ((!$1)); then
        local ahead behind
        local -a gitstatus

        behind=$(git rev-list HEAD..origin/${hook_com[branch]} 2>/dev/null | wc -l)
        (( $behind )) && gitstatus+=( "-${behind//[[:space:]]/}" )

        ahead=$(git rev-list origin/${hook_com[branch]}..HEAD 2>/dev/null | wc -l)
        (( $ahead )) && gitstatus+=( "+${ahead//[[:space:]]/}" )

        if [[ -n $gitstatus ]]; then
            if [[ -n ${hook_com[misc]} ]]; then
                hook_com[misc]+="$FX[bold]$chars[right-arrow]$FX[no-bold]"
            fi
            hook_com[misc]+="${(j:/:)gitstatus}"
        fi
    fi
}

function +vi-git-remotebranch() {
    if ((!$1)); then
        local remote

        # Are we on a remote-tracking branch?
        remote=${$(git rev-parse --verify ${hook_com[branch]}@{upstream} \
            --symbolic-full-name 2>/dev/null)/refs\/remotes\/}

        if [[ -n ${remote} ]] ; then
            hook_com[branch]="${hook_com[branch]}->${remote}"
        fi
    fi
}

function +vi-git-untracked() {
    if ((!$1)); then
        if [[ $(git rev-parse --is-inside-work-tree 2> /dev/null) == 'true' ]] && \
            git status --porcelain | grep '??' &> /dev/null ; then
            hook_com[unstaged]="new$FX[bold]$chars[right-arrow]$FX[no-bold]${hook_com[unstaged]}"
        fi
    fi
}

function prompt_bmoyles_precmd {
    emulate -L zsh
    zstyle -T ':prompt:bmoyles' vcs_info && vcs_info
}

function prompt_segment() {
    local bg fg dir
    typeset -x CURBG

    [[ -n $1 ]] && fg="%F{$1}" || fg="%f"
    [[ -n $2 ]] && bg="%K{$2}" || bg="%k"
    dir=$3

    if [[ $CURBG == 'NONE' ]] || [[ $1 == $CURBG ]] \
        || [[ $dir == 'right' ]]; then
        echo -n "%{$bg$fg%}"
    else
        if [[ $dir == 'left' ]]; then
            echo -n " %{$bg%F{$CURBG}%}$SEP%{$fg%} "
        fi
    fi

    [[ -n $4 ]] && echo -n ${(e)4}

    if [[ $dir == 'right' ]]; then
        if [[ $CURBG != 'NONE' ]] && [[ $2 != $CURBG ]]; then
            echo -n "%F{$2}$SEP"
        fi
    fi

    CURBG=$2
    return $CURBG
}

function prompt_end {
    local dir
    typeset -x CURBG

    dir=${1:-left}

    if [[ $CURBG != 'NONE' ]] ; then
        echo -n " %{%k%F{$CURBG}%}$SEP"
    else
        echo -n "%{%k%}"
    fi
    echo -n "%{%f%}"
    [[ $dir == 'left' ]] && CURBG='NONE'
}

function prompt_aws {
    local dir
    dir=${1:-left}
    local -r region=${AWS_DEFAULT_REGION:-${EC2_REGION:-}}

    if [[ -z ${region:-} ]] || [[ -z ${BASTION_ENVIRONMENT:-} ]]; then
        prompt_segment 220 24 $dir "aws:config unavailable"
        return
    fi
    #prompt_segment 24 255 $dir "aws:${FX[bold]}%F{226}${BASTION_ENVIRONMENT}%f\${fg}${FX[no-bold]}@${FX[bold]}%F{226}${region}%f\${fg}${FX[no-bold]}"
    prompt_segment 231 27 $dir "aws:${FX[bold]}${BASTION_ENVIRONMENT}${FX[no-bold]}@${FX[bold]}${region}${FX[no-bold]}\${fg}"
}

function prompt_vcs {
    local dir vcs_info_color
    local -a vcs_info_colors
    local maxexports last i

    dir=${1:-left}

    zstyle -s ":vcs_info:*" "max-exports" maxexports

    vcs_info_color=0
    vcs_info_colors=( 25 26 27 38 39 )
    for (( i = 0; i < maxexports; i++ ))
    do
        if [[ -n ${(P)${:-vcs_info_msg_${i}_}} ]]; then
            (( vcs_info_color++ ))
            prompt_segment 231 $vcs_info_colors[$vcs_info_color] $dir ${(P)${:-vcs_info_msg_${i}_}}
        fi
    done
}

function prompt_venv {
    local dir
    dir=${1:-left}
    #local -r venv_name="${VIRTUAL_ENV##*/}"
    local -r venv_name="${VIRTUAL_ENV}"
    prompt_segment 231 23 $dir "venv:${FX[bold]}${venv_name}${FX[no-bold]}"
}

function prompt_user {
    local dir
    dir=${1:-left}

    prompt_segment 0 208 $dir '%n'
}

function prompt_host {
    local dir
    dir=${1:-left}

    prompt_segment 0 202 $dir '%m'
}

function prompt_cwd {
    local dir
    dir=${1:-left}

    prompt_segment 231 22 $dir '%~'
}

function prompt_history {
    local dir
    dir=${1:-left}
    prompt_segment 0 214 $dir '%!'
}

function prompt_promptchar {
    local dir
    dir=${1:-left}

    prompt_segment 0 196 $dir '%#'
}

function prompt_date {
    local dir
    dir=${1:-left}

    prompt_segment 231 053 $dir " %D{%a %b %e} "
}

function prompt_time {
    local dir
    dir=${1:-left}

    prompt_segment 231 052 $dir " %T "
}

function prompt_bmoyles_build_prompt {
    emulate -L zsh

    typeset -x CURBG
    CURBG='NONE'
    SEP=$chars[right-arrow-filled]

    echo -n $prompt_newline
    if zstyle -t ':prompt:bmoyles' aws ; then
        prompt_aws
        prompt_end
        echo -n $prompt_newline
    fi
    if [[ -n $VIRTUAL_ENV ]]; then
        prompt_venv
    fi
    if zstyle -T ':prompt:bmoyles' vcs_info ; then
        prompt_vcs
    fi
    prompt_end
    echo -n $prompt_newline
    prompt_cwd
    prompt_end
    echo -n $prompt_newline
    prompt_history
    prompt_user
    prompt_host
    prompt_promptchar
    prompt_end
}

function prompt_bmoyles_build_rprompt {
    emulate -L zsh

    local tmp
    typeset -x CURBG='NONE'
    SEP=$chars[left-arrow-filled]

    tmp=$(prompt_time right)
    CURBG=$?
    tmp=$(prompt_date right)${tmp}
    CURBG=$?
    tmp=$(prompt_end right)${tmp}
    echo -n $tmp
}

prompt_bmoyles_setup $*

# vim: ft=zsh sw=4 sts=4 ts=4 expandtab
