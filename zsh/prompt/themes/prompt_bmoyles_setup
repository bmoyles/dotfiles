function prompt_bmoyles_setup {
    emulate -L zsh
    setopt localoptions
    prompt_opts=(cr percent subst)

    autoload -Uz add-zsh-hook
    add-zsh-hook precmd prompt_bmoyles_precmd

    autoload -Uz vcs_info
    zstyle ':vcs_info:*' enable git
    zstyle ':vcs_info:git*' check-for-changes true
    zstyle ':vcs_info:git*' unstagedstr "|$FX[bold]modified$FX[no-bold]|"
    zstyle ':vcs_info:git*' stagedstr "|$FX[bold]staged$FX[no-bold]|"
    zstyle ':vcs_info:git*' formats \
        "%b$chars[branch]%r(%m%u%c)"
    zstyle ':vcs_info:git*' actionformats \
        "%b$chars[branch]%r$chars[plus-minus]%a(%m%u%c)"
    zstyle ':vcs_info:git*+set-message:*' hooks git-st git-remotebranch git-untracked

    PROMPT='%{%f%b%k%}$(prompt_bmoyles_build_prompt)'
    RPROMPT='%{%f%b%k%}$(prompt_bmoyles_build_rprompt)'
}

function +vi-git-st() {
    local ahead behind
    local -a gitstatus

    behind=$(git rev-list HEAD..origin/${hook_com[branch]} 2>/dev/null | wc -l)
    (( $behind )) && gitstatus+=( $(prompt_segment magenta 0 left "$chars[downarrow]${behind//[[:space:]]/}") )

    ahead=$(git rev-list origin/${hook_com[branch]}..HEAD 2>/dev/null | wc -l)
    (( $ahead )) && gitstatus+=( $(prompt_segment magenta 0 left "$chars[uparrow]${ahead//[[:space:]]/}") )

    hook_com[misc]+=$FX[bold]${(j::)gitstatus}$FX[no-bold]
}

function +vi-git-remotebranch() {
    local remote

    # Are we on a remote-tracking branch?
    remote=${$(git rev-parse --verify ${hook_com[branch]}@{upstream} \
        --symbolic-full-name 2>/dev/null)/refs\/remotes\/}

    if [[ -n ${remote} ]] ; then
        hook_com[branch]="${hook_com[branch]} $chars[hook-right-arrow] ${remote}"
    fi
}

+vi-git-untracked(){
    if [[ $(git rev-parse --is-inside-work-tree 2> /dev/null) == 'true' ]] && \
        git status --porcelain | grep '??' &> /dev/null ; then
        # This will show the marker if there are any untracked files in repo.
        # If instead you want to show the marker only if there are untracked
        # files in $PWD, use:
        #[[ -n $(git ls-files --others --exclude-standard) ]] ; then
        hook_com[staged]+="|new|"
    fi
}

function prompt_bmoyles_precmd {
    emulate -L zsh
    zstyle -T ':prompt:bmoyles' vcs_info && vcs_info
}

function prompt_segment() {
    local bg fg dir
    typeset -x CURBG

    [[ -n $1 ]] && bg="%K{$1}" || bg="%k"
    [[ -n $2 ]] && fg="%F{$2}" || fg="%f"
    dir=$3

    if [[ $CURBG == 'NONE' ]] || [[ $1 == $CURBG ]] \
        || [[ $dir == 'right' ]]; then
        echo -n "%{$bg$fg%}"
    else
        if [[ $dir == 'left' ]]; then
            echo -n " %{$bg%F{$CURBG}%}$SEP%{$fg%} "
        fi
    fi

    [[ -n $4 ]] && print -Pn $4

    if [[ $dir == 'right' ]]; then
        if [[ $CURBG != 'NONE' ]] && [[ $1 != $CURBG ]]; then
            echo -n "%F{$1}$SEP"
        fi
    fi

    CURBG=$1
}

function prompt_end {
    local dir
    typeset -x CURBG

    dir=${1:-left}

    if [[ $CURBG != 'NONE' ]] ; then
        echo -n "%{%k%F{$CURBG}%}$SEP"
    else
        echo -n "%{%k%}"
    fi
    echo -n "%{%f%}"
    [[ $dir == 'left' ]] && CURBG='NONE'
}

function prompt_aws {
        local dir
        dir=$1
        [[ ! -r $AWS_CONFIG_FILE ]] && prompt_segment green red $dir "$chars[x]"
        prompt_segment green magenta $dir "aws:${FX[bold]}${AWS_CONFIG_FILE:t}${FX[no-bold]}@${FX[bold]}${AWS_DEFAULT_REGION}${FX[no-bold]}"
}

function prompt_bmoyles_build_prompt {
    emulate -L zsh

    typeset -x CURBG
    CURBG='NONE'
    SEP=$chars[right-arrow-filled]
    echo -n $prompt_newline
    prompt_segment blue 0 left "%n@%m"
    if zstyle -T ':prompt:bmoyles' aws ; then
        prompt_aws left
    fi
    prompt_end
    echo -n $prompt_newline

    prompt_segment 214 0 left '%~'
    if zstyle -T ':prompt:bmoyles' vcs_info ; then
        [[ -n $vcs_info_msg_0_ ]] && \
            prompt_segment magenta black left $vcs_info_msg_0_
    fi
    prompt_end
    echo -n $prompt_newline

    prompt_segment red 0 left '%(!.#.%%%%)'
    prompt_end
}

function prompt_bmoyles_build_rprompt {
    emulate -L zsh

    local tmp

    typeset -x CURBG='NONE'
    SEP=$chars[left-arrow-filled]

    tmp=$(prompt_segment 027 0 right "%D{%l:%M %p}")
    CURBG=027
    tmp=$(prompt_segment 033 0 right "%D{%a %b %e}")$tmp
    CURBG=033
    tmp=$(prompt_end right)$tmp
    echo -n $tmp
}

prompt_bmoyles_setup $*

# vim: ft=zsh sw=4 sts=4 ts=4 expandtab
