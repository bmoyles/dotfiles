_zshenv_set_distro() {
  emulate -L zsh
  if [[ ${OS} = linux ]]; then
    if [[ -f /etc/redhat-release ]]; then
      DISTRO=rhel
    elif [[ -f /etc/debian_version ]]; then
      DISTRO=deb
    fi
    if [[ -n "${DISTRO}" ]]; then
      readonly -x DISTRO
    fi
  fi
}

readonly -lx OS=$(uname -s)
_zshenv_set_distro; unset -f _zshenv_set_distro

typeset -ax _zsh_config_flavors
_zsh_config_flavors=( all ${OS:+${OS}} ${DISTRO:+${DISTRO}} )
readonly _zsh_config_flavors

typeset -ax _zsh_config_dirs
_zsh_config_dirs=( "${ZDOTDIR}" "${DOTLOCAL}/zsh" )
readonly _zsh_config_dirs

local -a _config_sections
_config_sections=( env path functions )

local _config_dir
local _config
for _config_dir in ${_zsh_config_dirs}; do
  for _config in ${_config_dir}/${^_config_sections}/${^_zsh_config_flavors}.zsh(N) ; do
    source ${_config}
  done
done

unset _config _config_dir _config_sections

# vim: ft=zsh sts=2 ts=2 sw=2 expandtab
